plugins {
	id "com.github.node-gradle.node" version "7.1.0"
}

apply plugin: 'java'

repositories {
	mavenCentral()
}

node {
	download = true
	version = "18.20.2"
	nodeProjectDir = file("${buildDir}/resources/test")
}

task startMockBackend(type: NpmTask) {
	args=["start"]
}

task stopMockBackend(type: NpmTask) {
	args=["stop"]
}

test {
	dependsOn npmInstall
	dependsOn startMockBackend
	useJUnitPlatform()
	finalizedBy stopMockBackend
}

sourceSets {
	test {
		resources {
			srcDir '../test'
		}
	}
}

npmInstall.dependsOn("processTestResources")

dependencies {
	implementation 'com.fasterxml.jackson.jr:jackson-jr-objects:2.20.0'
	implementation 'software.amazon.spapi:spapi-sdk:1.6.0'
	implementation 'org.threeten:threetenbp:1.6.5'
	testImplementation 'com.fasterxml.jackson.jr:jackson-jr-all:2.20.0'
	testImplementation 'org.junit.jupiter:junit-jupiter:5.13.4'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

}
